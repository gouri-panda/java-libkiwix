apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/cpp']
            jni.srcDirs = [] //disable automatic ndk-build
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'com.getkeepsafe.relinker:relinker:1.3.1'
}

task writePom {
  pom {
    project {
        groupId 'org.kiwix.kiwixlib'
        artifactId 'kiwixlib'
        version '10.0.0' + (System.env.KIWIXLIB_BUILDVERSION == null ? '' : '-'+System.env.KIWIXLIB_BUILDVERSION)
        packaging 'aar'
        name 'kiwixlib'
        url 'https://github.com/kiwix/kiwix-lib'
        licenses {
            license {
                name 'GPLv3'
                url 'https://www.gnu.org/licenses/gpl-3.0.en.html'
            }
        }
      developers {
          developer {
              id 'kiwix'
              name 'kiwix'
              email 'contact@kiwix.org'
          }
      }
      scm {
          connection 'https://github.com/kiwix/kiwix-lib.git'
          developerConnection 'https://github.com/kiwix/kiwix-lib.git'
          url 'https://github.com/kiwix/kiwix-lib'
      }
    }
  }.withXml {
    def dependenciesNode = asNode().appendNode('dependencies')

    //Iterate over the implementation dependencies, adding a <dependency> node for each
    configurations.implementation.allDependencies.each {
      def dependencyNode = dependenciesNode.appendNode('dependency')
      dependencyNode.appendNode('groupId', it.group)
      dependencyNode.appendNode('artifactId', it.name)
      dependencyNode.appendNode('version', it.version)
    }
  }.writeTo("$buildDir/pom.xml")
}

